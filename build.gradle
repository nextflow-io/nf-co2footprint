/*
 * Copyright 2021-2022, Seqera Labs
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    // Apply the groovy plugin to add support for Groovy
    id 'io.nextflow.nextflow-plugin' version '1.0.0-beta.10'
    id 'idea'
    id 'groovy'
}

group = 'io.nextflow'
// DO NOT SET THE VERSION HERE
// THE VERSION FOR PLUGINS IS DEFINED IN THE `/resources/META-INF/MANIFEST.NF` file

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

idea {
    module.inheritOutputDirs = true
}

repositories {
    mavenCentral()
    maven { url = 'https://jitpack.io' }
    maven { url = 'https://s3-eu-west-1.amazonaws.com/maven.seqera.io/releases' }
    maven { url = 'https://s3-eu-west-1.amazonaws.com/maven.seqera.io/snapshots' }
}

configurations {
    // see https://docs.gradle.org/4.1/userguide/dependency_management.html#sub:exclude_transitive_dependencies
    runtimeClasspath.exclude group: 'org.slf4j', module: 'slf4j-api'
}

sourceSets {
    main.java.srcDirs = []
    main.groovy.srcDirs = ['src/main']
    main.resources.srcDirs = ['src/resources']
    test.groovy.srcDirs = ['src/test']
    test.java.srcDirs = []
    test.resources.srcDirs = ['src/testResources']
}

// Versioning of important packages
ext {
    nextflowVersion = '24.10.6'
}
String groovyVersion = '4.0.27'
String pf4jVersion = '3.13.0'
String slf4jVersion = '2.0.17'
String argparseVersion = '0.9.0'

dependencies {
    // This dependency is exported to consumers, that is to say found on their compile classpath.
    compileOnly "io.nextflow:nextflow:$nextflowVersion"
    compileOnly "org.slf4j:slf4j-api:$slf4jVersion"
    compileOnly "org.pf4j:pf4j:$pf4jVersion"
    compileOnly "net.sourceforge.argparse4j:argparse4j:$argparseVersion"

    // add here plugins dependencies

    // test configuration
    testImplementation "org.apache.groovy:groovy-nio:$groovyVersion"
    testImplementation "io.nextflow:nextflow:$nextflowVersion"
    testImplementation ("org.apache.groovy:groovy-test:$groovyVersion") { exclude group: 'org.apache.groovy' }
    testImplementation ('cglib:cglib-nodep:3.3.0')
    testImplementation ('org.objenesis:objenesis:3.1')
    testImplementation ('org.spockframework:spock-core:2.3-groovy-4.0') { exclude group: 'org.apache.groovy'; exclude group: 'net.bytebuddy' }
    testImplementation ('org.spockframework:spock-junit4:2.3-groovy-4.0') { exclude group: 'org.apache.groovy'; exclude group: 'net.bytebuddy' }
    testImplementation ('com.google.jimfs:jimfs:1.1')
    testImplementation ('ch.qos.logback:logback-classic:1.5.18')

    testImplementation(testFixtures("io.nextflow:nextflow:$nextflowVersion"))
    testImplementation(testFixtures("io.nextflow:nf-commons:$nextflowVersion"))

    // see https://docs.gradle.org/4.1/userguide/dependency_management.html#sec:module_replacement
    modules {
        module('commons-logging:commons-logging') { replacedBy('org.slf4j:jcl-over-slf4j') }
    }
}

// Plugin version
version = '1.1.0'

nextflowPlugin {
    // Minimum Nextflow version
    nextflowVersion = '24.10.6'

    // Plugin metadata
    provider = 'nextflow'
    className = 'nextflow.co2footprint.CO2FootprintPlugin'
    extensionPoints = [
            'nextflow.co2footprint.CO2FootprintFactory'
    ]

}

jar {
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': version,
                'Implementation-Vendor': nextflowPlugin.provider,
                'Specification-Title': project.name,
                'Specification-Version': version,
                'Specification-Vendor': nextflowPlugin.provider
        )
    }
}


// use JUnit 5 platform
test {
    useJUnitPlatform()
    testLogging {
        showStandardStreams = true
    }
}
